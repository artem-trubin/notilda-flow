import material/material;

import note;

export {
  makeApp() -> MAppStructure;
}

makeApp() -> MAppStructure {
  mockTextForNotes = [
    "Test note",
    "Second test note",
    "Third super ttest note",
  ];

  notesB : DynamicBehaviour<[Note]> = make(
    map(
      mockTextForNotes,
      \noteText -> Note(makeNoteId(), noteText)
    )
  );

  addNote = \note : Note -> {
    next(notesB, arrayPush(getValue(notesB), note))
  };

  deleteNote = \noteId : string -> {
    next(
      notesB,
      filter(
        getValue(notesB),
        \note -> note.id != noteId
      )
    )
  };

  notesList = MSelect(
    notesB,
    \notes -> MLines(map(notes, \note -> makeNoteCard(note, deleteNote)))
  );

  content = MContent(
    MLines([
      notesList,
      AddNoteButton(addNote)
    ])
  );

  MAppStructure([content], []);
}
