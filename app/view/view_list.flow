import material/material;

import app/dialogs;
import app/note/note;
import app/note/note_card;
import app/note/note_helpers;
import app/state/state;
import app/view/view;

export {
  makeViewList(
    state : NotildaState,
  ) -> Material;
}

makeViewList(
  state : NotildaState,
) -> Material {
  openNote = \note -> {
    nextDistinct(state.currentNoteB, note);
    nextDistinct(state.currentViewB, ViewEditor());
  }

  createNote = \ -> {
    nextDistinct(state.currentNoteB, makeEmptyNote());
    nextDistinct(state.currentViewB, ViewEditor());
  }

  onDeleteClick = \note : Note -> {
    showConfirmationDialog(
      state.manager,
      "Are you sure you want to delete this note?",
      \ -> removeNote(state, note),
      nop,
    )
  }

  addNoteButton = MTextButton(
    "New Note",
    createNote,
    [],
    [],
  );

  makeNoteList = \notes : [Note] -> MLinesA(
    map(
      notes,
      \n -> makeNoteCard(n, openNote, onDeleteClick, false)
    )
  );

  MSelect(state.notesB, \notes : [Note] -> {
    MCenterX(
      MLinesA([
        addNoteButton,
        makeNoteList(notes),
      ])
    )
  });
}
