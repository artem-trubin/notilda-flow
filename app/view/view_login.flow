import material/material2tropic;

import app/user/user;
import app/user/user_helpers;
import app/view/view;

export {
  makeViewLogin(
    state : NotildaState,
  ) -> Material;
}

makeViewLogin(
  state : NotildaState,
) -> Material {
  inputWidth = 256.0;
  usernameInputTextB = make("");
  passwordInputTextB = make("");
  warningTextB = make("");

  tryLogin = \ -> {
    username = getValue(usernameInputTextB);
    password = getValue(passwordInputTextB);

    eitherFn(
      getUserByLoginPassword(state, username, password),
      \user -> { loginUser(state, user); next(state.currentViewB, ViewList()); },
      \ -> next(warningTextB, "User not found. Wrong username or password."),
    );
  }

  toRegistrationView = \ -> {
    next(state.currentViewB, ViewRegistration());
  }

  warning = MSelect(warningTextB, \text -> MText(text, [MRed(500)]));
  usernameInput = MTextInput(
    usernameInputTextB,
    [
      MWidth(inputWidth),
      MLabel("Username"),
      MFloatingLabel(),
    ],
    []
  );
  passwordInput = MTextInput(
    passwordInputTextB,
    [
      MWidth(inputWidth),
      MLabel("Password"),
      MFloatingLabel(),
    ],
    []
  );
  loginButton = MTextButton("LOGIN", tryLogin, [], [MShortcut("enter")]);
  toRegistrationButton = MTextButton(
    "TO REGISTRATION PAGE",
    toRegistrationView,
    [], []
  );

  MCenterY(
    MCenterX(
      MLinesXCenter([
        usernameInput,
        passwordInput,
        MCols([toRegistrationButton, loginButton]),
        warning,
      ])
    )
  );
}
