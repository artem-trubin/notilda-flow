import material/material2tropic;

import app/note/note;

export {
  makeEditor(
    currentNoteIndB : DynamicBehaviour<int>,
    notesB : DynamicBehaviour<[Note]>,
  ) -> Material;
}

makeEditor(
  currentNoteIndB : DynamicBehaviour<int>,
  notesB : DynamicBehaviour<[Note]>,
) -> Material {
  MSelect(currentNoteIndB, \currentNoteInd -> {
    if (currentNoteInd == -1) {
      MCenterX(MCenterY(MText("Select note to edit", [])));
    } else {
      notes = getValue(notesB);
      note = notes[currentNoteInd];
      noteTextB = make(note.text);

      MConstruct(
        [
          makeSubscribe(
            noteTextB,
            \t -> {
              next(
                notesB,
                map(notes, \n -> if (n.id == note.id) Note(note with text = t) else n)
              );
            }
          ),
        ],
        MTextInput(
          noteTextB,
          [],
          []
        )
      )
    }
  })
}
