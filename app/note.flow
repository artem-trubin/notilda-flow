import math/crypto;
import material/material2tropic;

export {
  Note(
    id : string,
    text : string,
  );

  makeNoteId() -> string;

  AddNoteButton(
    onDone : (Note) -> void,
  ) -> MTextButton;

  makeNoteCard(
    note : Note,
    deleteCard : (noteId : string) -> void,
    startEditingCard : (noteId : string) -> void,
  ) -> MCard;
}

Note(
  id : string,
  text : string,
);

makeNoteCard(
  note : Note,
  deleteCard : (noteId : string) -> void,
  startEditingCard : (noteId : string) -> void,
) -> MCard {
  noteText = MSupportingTextBlock(
    note.text,
    []
  );

  noteButtons = MActionBlock(
    [
      MTextButton("Edit", \ -> startEditingCard(note.id), [], [])
    ],
    [
      MTextButton("Delete", \ -> deleteCard(note.id), [], [])
    ],
    []
  );

  blocks = [
    noteText,
    noteButtons,
  ];

  style = [
    MCardWidth(256.0),
  ];

  MCard(blocks, style, [])
}

makeNoteId() -> string {
  generateUID("", "", 16)
}

createNote(
  text : string,
  onDone : (Note) -> void
) -> void {
  newId = makeNoteId();
  note = Note(newId, text);

  onDone(note)
}

AddNoteButton(
  onDone : (Note) -> void,
) -> MTextButton {
  MTextButton(
    "Add Note",
    \ -> {
      createNote(
        "Some default text",
        onDone
      )
    },
    [],
    []
  )
}
