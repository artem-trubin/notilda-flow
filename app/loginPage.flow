import material/material2tropic;

import app/state;

export {
  makePageLogin(state : State) -> Material;
}

makePageLogin(state : State) -> Material {

  login = \username : string, password : string -> {
    foundUserM = find(
      getValue(state.usersB),
      \user -> user.name == username
    );
    eitherFn(
      foundUserM,
      \user -> if (user.password == password) {
        next(state.userB, user);
        next(state.currentPageB, PageTypeVault());
      } else {println("Wrong password")},
      \ -> {println("No such user")}
    )
  };

  usernameB = make("");
  usernameInput = MTextInput(usernameB, [MLabel("username"), MFloatingLabel()], []);

  passwordB = make("");
  passwordInput = MTextInput(passwordB, [MLabel("Password"), MFloatingLabel()], []);

  loginButton = MTextButton(
    "Login",
    \ -> login(
      getValue(usernameB),
      getValue(passwordB),
    ),
    [],
    []
  );

  MLines([
    usernameInput,
    passwordInput,
    loginButton,
  ]);
}
