import material/material2tropic;

import app/state;
import app/editor;

export {
  makePageVault(state : State) -> Material;
}

makePageVault(state : State) -> Material {
  mockTextForNotes = [
    "Test note",
    "Second test note",
    "Third super ttest note",
  ];

  next(
    state.notesB,
    map(mockTextForNotes, \noteText -> Note(makeNoteId(), noteText))
  );

  // Note functions

  addNote = \note : Note -> {
    next(state.notesB, arrayPush(getValue(state.notesB), note))
  };

  deleteNote = \noteId : string -> {
    next(
      state.notesB,
      filter(
        getValue(state.notesB),
        \note -> note.id != noteId
      )
    )
  };

  startEditingNote = \noteId : string -> {
    noteToEditM = find(getValue(state.notesB), \note -> note.id == noteId);

    eitherFn(
      noteToEditM,
      \noteToEdit -> {
        next(state.currentNoteMB, Some(noteToEdit))
      },
      \ -> println("Error: can't find not with such ID")
    )
  };

  saveNote = \updatedNote : Note -> {
    next(
      state.notesB,
      map(
        getValue(state.notesB),
        \note -> if (note.id == updatedNote.id) updatedNote else note
      )
    )
  };

  // UI elements

  notesList = MSelect(
    state.notesB,
    \notes -> MLines(
      map(
        notes,
        \note -> makeNoteCard(
          note,
          deleteNote,
          startEditingNote,
        )
      )
    )
  );

  MCols([
    MLines([
      notesList,
      AddNoteButton(addNote),
    ]),
    NoteEditor(state.currentNoteMB, saveNote),
  ]);
}
