import math/crypto;

import app/note/note;
import app/state/state;

export {
  addOrUpdateNote(
    state : NotildaState,
    note : Note,
  ) -> void;

  removeNote(
    state : NotildaState,
    note : Note,
  ) -> void;

  resetCurrentNote(
    state : NotildaState,
  ) -> void;
}

generateNoteId() -> string {
  generateUID("", "", 16);
}

addOrUpdateNote(
  state : NotildaState,
  note : Note,
) -> void {
  notes = getValue(state.notesB);
  currentUser = getValue(state.currentUserB);
  newNote = if (note.id == "") {
    Note(note with id = generateNoteId(), authorId = currentUser.id);
  } else {
    note;
  }
  newNotes = eitherFn(
    find(notes, \oldNote -> oldNote.id == newNote.id),
    \oldNote -> map(notes, \n -> if (n.id == newNote.id) newNote else n),
    \ -> arrayPush(notes, newNote)
  );

  nextDistinct(state.notesB, newNotes);
}

removeNote(
  state : NotildaState,
  note : Note,
) -> void {
  notes = getValue(state.notesB);
  newNotes = filter(notes, \n -> n.id != note.id);

  nextDistinct(state.notesB, newNotes);
}

resetCurrentNote(
  state : NotildaState,
) -> void {
  nextDistinct(state.currentNoteB, makeEmptyNote());
}
