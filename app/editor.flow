import material/material2tropic;

import app/note;

export {
  NoteEditor(
    currentNoteMB : DynamicBehaviour<Maybe<Note>>,
    saveNote : (updatedNote : Note) -> void,
  ) -> Material;
}

NoteEditor(
  currentNoteMB : DynamicBehaviour<Maybe<Note>>,
  saveNote : (updatedNote : Note) -> void,
) -> Material {
  MSelect(
    currentNoteMB,
    \currentNoteM -> eitherMap(
      currentNoteM,
      \currentNote -> {
        textB = make(currentNote.text);

        editorInput = MTextInput(
          textB,
          [
            Multiline(true),
            MMaxLines(10),
            MOutlined(),
          ],
          []
        );

        MLines([
          editorInput,
          MCols([
            MFillX(),
            MTextButton(
              "Save",
              \ -> saveNote(Note(currentNote with text = getValue(textB))),
              [],
              []
            )
          ])
        ])
      },
      MText("Please choose a note to edit", [])
    )
  );
}
